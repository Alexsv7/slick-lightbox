###
# SlickLightbox documentation #

Documentation generated by [CoffeeDoc](http://github.com/omarkhan/coffeedoc)
###

class SlickLightbox
	###
	The one and only class used.
	###
	constructor: (element, @options) ->
		### Binds the plugin. ###
		@element = $(element)
		@options ?= {}
		@element.on 'click.slickLightbox', 'a', (e) =>
			e.preventDefault()
			@init()
	init: ->
		### Creates the lightbox, opens it, binds events and calls `slick`. ###
		# @destroyPrevious()
		@createModal()
		@open()
		@bindEvents()
		@slick()
	createModal: ->
		### Creates a `slick`-friendly modal with a simple `ul`. ###
		links = $.map @element.find('a'), (el) ->
			"""<div class="slick-lightbox-slick-item"><img class="slick-lightbox-slick-img" src="#{ el.href }" /></div>"""
		html = """
		<div class="slick-lightbox slick-hide">
			<div class="slick-lightbox-inner">
				<div class="slick-lightbox-slick">#{ links.join('') }</div>
				<button type="button" class="slick-lightbox-close"></button>
			<div>
		<div>
		"""
		@modalElement = $(html)
		$('body').append @modalElement
	slick: ->
		### Runs slick by default, using `options.slick` if provided. If `options.slick` is a function, it gets fired instead of us initializing slick. ###
		if @options.slick?
			if typeof @options.slick is 'function'
				@options.slick @modalElement
			else
				@modalElement.find('.slick-lightbox-slick').slick @options.slick
		else
			@modalElement.find('.slick-lightbox-slick').slick()
		@modalElement.trigger 'init.slickLightbox'
	open: ->
		### Opens the lightbox. ###
		@modalElement.removeClass('slick-hide')
	close: ->
		### Closes the lightbox and destroys it, maintaining the original element bindings. ###
		@modalElement.addClass('slick-hide')
		@destroy()
	bindEvents: ->
		### Binds global events. ###
		resizeSlides = =>
			@modalElement.find('.slick-lightbox-slick-item').height @modalElement.find('.slick-lightbox-inner').height()
		$(window).on 'orientationchange.slickLightbox resize.slickLightbox', resizeSlides
		@modalElement.on 'init.slickLightbox', resizeSlides
		@modalElement.on 'destroy.slickLightbox', => @destroy()
		@modalElement.on 'click.slickLightbox touchstart.slickLightbox', '.slick-lightbox-close', (e) =>
			e.preventDefault()
			@close()
	unbindEvents: ->
		### Unbinds global events. ###
		$(window).off '.slickLightbox'
		@modalElement.off '.slickLightbox'
	destroy: (unbindAnchors = false) ->
		### Destroys the lightbox and unbinds global events. If `true` is passed as an argument, unbinds the original element as well. ###
		@unbindEvents()
		@modalElement.remove()
		if unbindAnchors
			@element.off '.slickLightbox'
	destroyPrevious: ->
		### Destroys lightboxes currently in DOM. ###
		$('body').children('.slick-lightbox').trigger 'destroy.slickLightbox'

# jQuery method
$.fn.slickLightbox = (options) ->
	this.slickLightbox = new SlickLightbox this, options
$.fn.unslickLightbox = (options) ->
	this.slickLightbox.destroy(true)
